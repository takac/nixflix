{
  pkgs,
  config,
  lib,
  ...
}:
with lib;
let
  secrets = import ../lib/secrets { inherit lib; };
  cfg = config.nixflix.mullvad;
  mullvadPkg = if cfg.gui.enable then pkgs.mullvad-vpn else pkgs.mullvad;
in
{
  options.nixflix.mullvad = {
    enable = mkOption {
      default = false;
      example = true;
      description = ''
        Whether to enable Mullvad VPN.

        #### Using Tailscale with Mullvad

        Set `nixflix.mullvad.tailscale.enable = true` to automatically configure
        nftables rules that route Tailscale traffic around the VPN tunnel.

        By default, all Tailscale traffic (mesh and exit node) bypasses Mullvad.
        To route exit node traffic through Mullvad while keeping mesh traffic
        direct, also set `nixflix.mullvad.tailscale.exitNode = true`.
      '';
      type = types.bool;
    };

    accountNumber = secrets.mkSecretOption {
      default = null;
      description = "Mullvad account number.";
    };

    gui = {
      enable = mkEnableOption "Mullvad GUI application";
    };

    location = mkOption {
      type = types.listOf types.str;
      default = [ ];
      example = [
        "us"
        "nyc"
      ];
      description = ''
        Mullvad server location as a list of strings.

        Format: `["country"]` | `["country" "city"]` | `["country" "city" "full-server-name"]` | `["full-server-name"]`

        Examples: `["us"]`, `["us" "nyc"]`, `["se" "got" "se-got-wg-001"]`, `["se-got-wg-001"]`

        Use "mullvad relay list" to see available locations.
        Leave empty to use automatic location selection.
      '';
    };

    enableIPv6 = mkOption {
      type = types.bool;
      default = false;
      description = "Wether to enable IPv6 for Mullvad";
    };

    dns = mkOption {
      type = types.listOf types.str;
      default = [
        "1.1.1.1"
        "1.0.0.1"
        "8.8.8.8"
        "8.8.4.4"
      ];
      defaultText = literalExpression ''["1.1.1.1" "1.0.0.1" "8.8.8.8" "8.8.4.4"]'';
      example = [
        "194.242.2.4"
        "194.242.2.3"
      ];
      description = ''
        DNS servers to use with the VPN.
        Defaults to Cloudflare (1.1.1.1, 1.0.0.1) and Google (8.8.8.8, 8.8.4.4) DNS servers.
      '';
    };

    autoConnect = mkOption {
      type = types.bool;
      default = true;
      description = "Automatically connect to VPN on startup";
    };

    killSwitch = {
      enable = mkEnableOption "VPN kill switch (lockdown mode) - blocks all traffic when VPN is down";

      allowLan = mkOption {
        type = types.bool;
        default = true;
        description = "Allow LAN traffic when VPN is down (only effective with kill switch enabled)";
      };
    };

    persistDevice = mkOption {
      type = types.bool;
      default = false;
      description = ''
        Keep the Mullvad device logged in across service restarts and reboots.

        When false (default), the service logs out and revokes the device on stop,
        requiring a fresh login on next start. This frees up device slots (Mullvad
        allows 5 devices per account).

        When true, the service only disconnects on stop without logging out. The
        device identity persists in `/etc/mullvad-vpn/settings.json`. This is
        recommended when using the kill switch, as it avoids a deadlock on boot
        where the kill switch blocks network access before the daemon can log in.
      '';
    };

    tailscale = {
      enable = mkOption {
        type = types.bool;
        default = false;
        description = ''
          Automatically configure Tailscale to coexist with Mullvad VPN.

          Adds nftables rules to bypass Mullvad for Tailscale mesh traffic
          (100.64.0.0/10) and the Tailscale WireGuard port (UDP 41641).

          Requires `services.tailscale.enable = true`.
        '';
      };

      exitNode = mkOption {
        type = types.bool;
        default = false;
        description = ''
          Route Tailscale exit node traffic through Mullvad.

          When false (default), all Tailscale traffic bypasses Mullvad entirely.

          When true, direct mesh traffic (device to device) and Tailscale protocol
          traffic still bypass Mullvad, but exit node traffic (internet-bound traffic
          from Tailscale clients) is routed through the Mullvad VPN tunnel.
        '';
      };
    };
  };

  config = mkIf cfg.enable {
    services.resolved.enable = true;

    services.mullvad-vpn = {
      enable = true;
      enableExcludeWrapper = true;
      package = mullvadPkg;
    };

    systemd.services.mullvad-config = {
      description = "Configure Mullvad VPN settings";
      wantedBy = [ "multi-user.target" ];
      after = [
        "mullvad-daemon.service"
        "network-online.target"
      ];
      requires = [
        "mullvad-daemon.service"
        "network-online.target"
      ];
      serviceConfig = {
        Type = "oneshot";
        RemainAfterExit = true;
        ExecStart = pkgs.writeShellScript "configure-mullvad" ''
          for i in {1..30}; do
            if ${mullvadPkg}/bin/mullvad status &>/dev/null; then
              echo "Mullvad daemon is ready with relay list available"
              sleep 2
              break
            fi
            sleep 1
          done

          ${optionalString (cfg.accountNumber != null) ''
            if ${mullvadPkg}/bin/mullvad account get | grep -q "Not logged in"; then
              echo "Logging in to Mullvad account..."
              echo "${secrets.toShellValue cfg.accountNumber}" | ${mullvadPkg}/bin/mullvad account login

              echo "Waiting for relay list to download..."
              for i in {1..30}; do
                RELAY_COUNT=$(${mullvadPkg}/bin/mullvad relay list 2>/dev/null | wc -l)
                if [ "$RELAY_COUNT" -gt 10 ]; then
                  echo "Relay list populated ($RELAY_COUNT lines)"
                  sleep 1
                  break
                fi
                sleep 1
              done
            fi
          ''}

          ${mullvadPkg}/bin/mullvad dns set custom ${concatStringsSep " " cfg.dns}

          ${mullvadPkg}/bin/mullvad tunnel set ipv6 ${if cfg.enableIPv6 then "on" else "off"}

          ${optionalString (cfg.location != [ ]) ''
            ${mullvadPkg}/bin/mullvad relay set location ${escapeShellArgs cfg.location}
          ''}

          ${optionalString cfg.killSwitch.enable ''
            ${mullvadPkg}/bin/mullvad lockdown-mode set on
            ${mullvadPkg}/bin/mullvad lan set ${if cfg.killSwitch.allowLan then "allow" else "block"}
          ''}

          ${optionalString (!cfg.killSwitch.enable) ''
            ${mullvadPkg}/bin/mullvad lockdown-mode set off
          ''}

          ${optionalString cfg.autoConnect ''
            ${mullvadPkg}/bin/mullvad auto-connect set on
            ${mullvadPkg}/bin/mullvad connect
          ''}

          ${optionalString (!cfg.autoConnect) ''
            ${mullvadPkg}/bin/mullvad auto-connect set off
          ''}
        '';
        ExecStop =
          if cfg.persistDevice then
            pkgs.writeShellScript "disconnect-mullvad" ''
              ${mullvadPkg}/bin/mullvad disconnect || true
            ''
          else
            pkgs.writeShellScript "logout-mullvad" ''
              DEVICE_NAME=$(${mullvadPkg}/bin/mullvad account get | grep "Device name:" | sed 's/.*Device name:[[:space:]]*//')
              if [ -n "$DEVICE_NAME" ]; then
                echo "Revoking device: $DEVICE_NAME"
                ${mullvadPkg}/bin/mullvad account revoke-device "$DEVICE_NAME" || true
              fi
              ${mullvadPkg}/bin/mullvad account logout  || true
              ${mullvadPkg}/bin/mullvad disconnect || true
            '';
      };
    };

    assertions = [
      {
        assertion = cfg.tailscale.enable -> config.services.tailscale.enable;
        message = "nixflix.mullvad.tailscale.enable requires services.tailscale.enable = true";
      }
      {
        assertion = cfg.tailscale.exitNode -> cfg.tailscale.enable;
        message = "nixflix.mullvad.tailscale.exitNode requires nixflix.mullvad.tailscale.enable = true";
      }
    ];

    networking.nftables.enable = mkIf cfg.tailscale.enable true;

    networking.nftables.tables."mullvad-tailscale" = mkIf cfg.tailscale.enable {
      enable = true;
      family = "inet";
      content =
        if cfg.tailscale.exitNode then
          ''
            chain prerouting {
              type filter hook prerouting priority -50; policy accept;

              # Allow Tailscale protocol traffic to bypass Mullvad
              udp dport 41641 ct mark set 0x00000f41 meta mark set 0x6d6f6c65;

              # Allow direct mesh traffic (Tailscale device to Tailscale device) to bypass Mullvad
              ip saddr 100.64.0.0/10 ip daddr 100.64.0.0/10 ct mark set 0x00000f41 meta mark set 0x6d6f6c65;

              # Exit node traffic: DON'T mark it - let it route through VPN without bypass mark
              iifname "tailscale0" ip daddr != 100.64.0.0/10 meta mark set 0;

              # Return traffic from VPN: Mark it so it routes via Tailscale table
              iifname "wg0-mullvad" ip daddr 100.64.0.0/10 ct mark set 0x00000f41 meta mark set 0x6d6f6c65;
            }

            chain outgoing {
              type route hook output priority -100; policy accept;
              meta mark 0x80000 ct mark set 0x00000f41 meta mark set 0x6d6f6c65;
              ip daddr 100.64.0.0/10 ct mark set 0x00000f41 meta mark set 0x6d6f6c65;
              udp sport 41641 ct mark set 0x00000f41 meta mark set 0x6d6f6c65;
            }

            chain postrouting {
              type nat hook postrouting priority 100; policy accept;

              # Masquerade exit node traffic going through Mullvad
              iifname "tailscale0" oifname "wg0-mullvad" masquerade;
            }
          ''
        else
          ''
            chain prerouting {
              type filter hook prerouting priority -100; policy accept;
              ip saddr 100.64.0.0/10 ct mark set 0x00000f41 meta mark set 0x6d6f6c65;
              udp dport 41641 ct mark set 0x00000f41 meta mark set 0x6d6f6c65;
            }

            chain outgoing {
              type route hook output priority -100; policy accept;
              meta mark 0x80000 ct mark set 0x00000f41 meta mark set 0x6d6f6c65;
              ip daddr 100.64.0.0/10 ct mark set 0x00000f41 meta mark set 0x6d6f6c65;
              udp sport 41641 ct mark set 0x00000f41 meta mark set 0x6d6f6c65;
            }
          '';
    };
  };
}
